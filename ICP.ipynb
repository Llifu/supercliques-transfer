{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 37,
   "id": "6d2ab428",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LightGraphs\n",
    "using LinkedLists\n",
    "using Combinatorics\n",
    "using Random"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 52,
   "id": "bce226f5",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "mcs"
      ]
     },
     "execution_count": 52,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\"\"\"\n",
    "    readgraph(file = stdin, undirected = false)\n",
    "\n",
    "Read a graph from the standard input or a given file and return a directed simple graph.\n",
    "In case undirected graphs are read (as in the toy examples we give\n",
    "here), the argument undirected=true may be passed. Then, each edge can be given only once.\n",
    "\"\"\"\n",
    "function readgraph(file = stdin, undirected = false)\n",
    "    if file != stdin\n",
    "        file = open(file, \"r\")\n",
    "    end\n",
    "    (n, m) = parse.(Int, split(readline(file)))\n",
    "    readline(file)\n",
    "    G = SimpleDiGraph(n)\n",
    "    for i = 1:m\n",
    "        (a, b) = parse.(Int, split(readline(file)))\n",
    "        add_edge!(G, a, b)\n",
    "        undirected && add_edge!(G, b, a)\n",
    "    end\n",
    "    return G\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    ischordal(g)\n",
    "\n",
    "Return true if the given graph is chordal\n",
    "\"\"\"\n",
    "function ischordal(G)\n",
    "    mcsorder, invmcsorder, _ = mcs(G, Set())\n",
    "    \n",
    "    n = length(mcsorder)\n",
    "    \n",
    "    f = zeros(Int, n)\n",
    "    index = zeros(Int, n)\n",
    "    for i=n:-1:1\n",
    "        w = mcsorder[i]\n",
    "        f[w] = w\n",
    "        index[w] = i\n",
    "        for v in neighbors(G, w)\n",
    "            if invmcsorder[v] > i\n",
    "                index[v] = i\n",
    "                if f[v] == v\n",
    "                    f[v] = w\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        for v in neighbors(G, w)\n",
    "            if invmcsorder[v] > i\n",
    "                if index[f[v]] > i\n",
    "                    return false\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return true\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    vispush!(l::LinkedList, pointers, x, vis)\n",
    "\"\"\"\n",
    "@inline function vispush!(l::LinkedList, pointers, x, vis)\n",
    "    if vis\n",
    "        pointers[x] = push!(l,x)\n",
    "    else\n",
    "        pointers[x] = pushfirst!(l,x)\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    phi(cliquesize, i, fp, fmemo, pmemo)\n",
    "Recursively compute the function phi. Initial call is phi(cliquesize, 1, fp, fmemo, pmemo). \n",
    "If not used before, fmemo and pmemo should be zero vectors of appropriate size. \n",
    "\"\"\"\n",
    "function phi(cliquesize, i, fp, fmemo, pmemo)\n",
    "    pmemo[i] != 0 && return pmemo[i]\n",
    "    sum = fac(cliquesize-fp[i], fmemo)\n",
    "    for j = (i+1):length(fp)\n",
    "        sum -= fac(fp[j]-fp[i], fmemo) * phi(cliquesize, j, fp, fmemo, pmemo)\n",
    "    end\n",
    "    return pmemo[i] = sum\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    fac(n, fmemo)\n",
    "Recursively compute the factorial of n using memoization table fmemo.\n",
    "\"\"\"\n",
    "function fac(n, fmemo)\n",
    "    fmemo[n] != 0 && return fmemo[n]\n",
    "    n == 1 && return BigInt(1)\n",
    "    res = fac(n-1, fmemo) * n\n",
    "    return fmemo[n] = res\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    mcs(G, K)\n",
    "Performs a Maximum Cardinality Search on graph G. \n",
    "The elements of K are of prioritised and chosen first. \n",
    "Returns the visit order of the vertices, its inverse and the subgraphs C_G(K) (see Def. 1 in [1,2]). \n",
    "If K is empty a normal MCS is performed.\n",
    "The MCS takes the role of the LBFS in [1,2]. Details will be published in future work.\n",
    "\"\"\"\n",
    "function mcs(G, K)\n",
    "    n = nv(G)\n",
    "    copy_K = copy(K)\n",
    "    \n",
    "    # data structures for MCS\n",
    "    sets = [LinkedList{Int}() for i = 1:n+1]\n",
    "    pointers = Vector(undef,n)\n",
    "    size = Vector{Int}(undef, n)\n",
    "    visited = falses(n)\n",
    "    \n",
    "    # output data structures\n",
    "    mcsorder = Vector{Int}(undef, n)\n",
    "    invmcsorder = Vector{Int}(undef, n)\n",
    "    subgraphs = Array[]\n",
    "\n",
    "    # init\n",
    "    visited[collect(copy_K)] .= true\n",
    "    for v in vertices(G)\n",
    "        size[v] = 1\n",
    "        vispush!(sets[1], pointers, v, visited[v])\n",
    "    end\n",
    "    maxcard = 1\n",
    "\n",
    "    for i = 1:n\n",
    "        # first, the vertices in K are chosen\n",
    "        # they are always in the set of maximum cardinality vertices\n",
    "        if !isempty(copy_K)\n",
    "            v = pop!(copy_K)\n",
    "        # afterwards, the algorithm chooses any vertex from maxcard\n",
    "        else\n",
    "            v = first(sets[maxcard])\n",
    "        end\n",
    "        # v is the ith vertex in the mcsorder\n",
    "        mcsorder[i] = v\n",
    "        invmcsorder[v] = i\n",
    "        size[v] = -1\n",
    "\n",
    "        # immediately append possible subproblems to the output\n",
    "        if !visited[v]\n",
    "            vertexset = Vector{Int}()\n",
    "            for x in sets[maxcard]\n",
    "                visited[x] && break\n",
    "                visited[x] = true\n",
    "                push!(vertexset, x)\n",
    "            end\n",
    "            sg = induced_subgraph(G, vertexset)\n",
    "            subgraphs = vcat(subgraphs, (map(x -> sg[2][x], connected_components(sg[1]))))\n",
    "        end\n",
    "\n",
    "        deleteat!(sets[maxcard], pointers[v])\n",
    "\n",
    "        # update the neighbors\n",
    "        for w in inneighbors(G, v)\n",
    "            if size[w] >= 1\n",
    "                deleteat!(sets[size[w]], pointers[w])\n",
    "                size[w] += 1\n",
    "                vispush!(sets[size[w]], pointers, w, visited[w])\n",
    "            end\n",
    "        end\n",
    "        maxcard += 1\n",
    "        while maxcard >= 1 && isempty(sets[maxcard])\n",
    "            maxcard -= 1\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return mcsorder, invmcsorder, subgraphs\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 53,
   "id": "54d38c4e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "cliquetree"
      ]
     },
     "execution_count": 53,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\"\"\"\n",
    "    cliquetreefrommcs(G, mcsorder, invmcsorder)\n",
    "\"\"\"\n",
    "function cliquetreefrommcs(G, mcsorder, invmcsorder)\n",
    "    n = nv(G)\n",
    "    # data structures for the algorithm\n",
    "    K = Vector{Set}()\n",
    "    push!(K, Set())\n",
    "    s = 1\n",
    "    edgelist = Vector{Vector}()\n",
    "    visited = falses(n)\n",
    "    clique = zeros(Int, n)\n",
    "    \n",
    "    # seprators for the algorithm\n",
    "    Sep = Vector{Set}()\n",
    "    push!(Sep, Set())\n",
    "    \n",
    "    # chile clique\n",
    "    cclique = [Vector{Int}()]\n",
    "    \n",
    "    for i = 1:n\n",
    "        x = mcsorder[i]\n",
    "        S = Set{Int}()\n",
    "        for w in inneighbors(G, x)\n",
    "            if visited[w]\n",
    "                push!(S, w)\n",
    "            end\n",
    "        end\n",
    "        \n",
    "        # if necessary create new maximal clique\n",
    "        if K[s] != S\n",
    "            s += 1\n",
    "            push!(cclique, []) # get child clique\n",
    "            push!(K, S)\n",
    "            push!(Sep, copy(S))\n",
    "            k, _ = findmax(map(x -> invmcsorder[x], collect(S)))\n",
    "            p = clique[mcsorder[k]]\n",
    "            push!(edgelist, [p,s])\n",
    "            push!(cclique[p], s) # get child clique\n",
    "        end\n",
    "        \n",
    "        push!(K[s], x)\n",
    "        clique[x] = s \n",
    "        visited[x] = true;\n",
    "    end\n",
    "    K[1] = Set{Int}(K[1])\n",
    "    Sep[1] = Set{Int}(Sep[1])\n",
    "    return K, Sep, edgelist, s, cclique\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    cliquetree(G)\n",
    "\"\"\"\n",
    "function cliquetree(G)\n",
    "    mcsorder, invmcsorder, _ = mcs(G, Set())\n",
    "    K, Sep, edgelist, s, cclique = cliquetreefrommcs(G, mcsorder, invmcsorder)\n",
    "    return K, Sep, edgelist, s, cclique\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "72129108",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "SCTrans"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\"\"\"\n",
    "    SCTrans(K, Sep, edgelist, s)\n",
    "\n",
    "Generate the set of undirected connected components C_G(K_1), ...,C_G(K_m)\n",
    "\"\"\"\n",
    "function SCTrans(K, Sep, edgelist, s)\n",
    "    # Initial the structures of undirected connected components\n",
    "    Comp = [Set{Int}() for _ in 1:s, _ in 1:s]\n",
    "    \n",
    "    if !isempty(edgelist)\n",
    "        pa = edgelist[1][1]\n",
    "        Comp[2, pa] = setdiff(K[pa], Sep[2])\n",
    "        Comp[1, 2] = setdiff(K[2], Sep[2])\n",
    "\n",
    "        for i = 3 : s\n",
    "            ipa = edgelist[i-1][1]\n",
    "            Comp[i, 1:s] = Comp[ipa, 1:s]\n",
    "            Comp[i, ipa] = setdiff(K[ipa], Sep[i])\n",
    "            Comp[1, i] = setdiff(K[i], Sep[i]) \n",
    "            \n",
    "            for j = reverse(2: i-1)\n",
    "                Comp[j, i] = Comp[1, i]\n",
    "                Sep[i] ⊊ Sep[j] && begin\n",
    "                    # K_j and K_jpa belongs to one super clique in T^K_i\n",
    "                    jpa = edgelist[j-1][1]\n",
    "                    Comp[i, jpa] = union(Comp[i, jpa], Comp[i, j])\n",
    "                    Comp[i, j] = Set()\n",
    "                end\n",
    "            end\n",
    "            \n",
    "            tag = []\n",
    "            while ipa != 1 \n",
    "                Sep[ipa] ⊊ Sep[i] && begin\n",
    "                    # K_i and K_ipa belongs to one super clique in T^K_k\n",
    "                    push!(tag, ipa)\n",
    "                    for k in 1: ipa-1\n",
    "                        Comp[k, ipa] =  Comp[k, i] = union(Comp[k, ipa], Comp[k, i])\n",
    "                    end\n",
    "                end\n",
    "                ipa = edgelist[ipa-1][1]\n",
    "            end\n",
    "            \n",
    "            !isempty(tag) && begin\n",
    "                for k in 1: tag[1]-1\n",
    "                    Comp[k, i] = Set()\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    B = []\n",
    "    for i in 1:s\n",
    "        # Extract all non empty sets from the i-th row\n",
    "        non_empty_elements = collect(filter(!isempty, Comp[i, :])) |> x -> [collect(s) for s in x]\n",
    "\n",
    "        # Store non empty sets in B\n",
    "        push!(B, non_empty_elements)\n",
    "    end\n",
    "    return B\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 54,
   "id": "8523cefc",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "icount"
      ]
     },
     "execution_count": 54,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\"\"\"\n",
    "    icount(cc, memo, fmemo)\n",
    "\"\"\"\n",
    "function icount(cc, memo, fmemo, loop)\n",
    "    G = cc[1] # graph\n",
    "    mapping = cc[2] # mapping to original vertex numbers\n",
    "    n = nv(G)\n",
    "    \n",
    "    # check memoization table\n",
    "    mapG = Set(map(x -> mapping[x], vertices(G)))\n",
    "    haskey(memo, mapG) && return memo[mapG]\n",
    "    \n",
    "    # do bfs over the clique tree \n",
    "    K, Sep, edgelist, s, cclique = cliquetree(G) \n",
    "    Comp = SCTrans(K, Sep, edgelist, s)\n",
    "    sum = BigInt(0)\n",
    "    Q = [1]\n",
    "    vis = falses(s) \n",
    "    vis[1] = true\n",
    "    pred = -1 * ones(Int, s) \n",
    "    while !isempty(Q)\n",
    "        v = pop!(Q)\n",
    "        for x in cclique[v] # [edge[2] for edge in edgelist if edge[1] == v]\n",
    "            if !vis[x]\n",
    "                push!(Q, x)\n",
    "                vis[x] = true\n",
    "                pred[x] = v\n",
    "            end\n",
    "        end\n",
    "\n",
    "        prod = BigInt(1)\n",
    "        for H in Comp[v]\n",
    "            HH = induced_subgraph(G, H)\n",
    "            prod *= icount((HH[1], map(x -> mapping[x], HH[2])), memo, fmemo, loop+1)\n",
    "            #end\n",
    "        end\n",
    "        # compute correction term phi\n",
    "        FP = []\n",
    "        curr = v\n",
    "        curr_succ = -1\n",
    "        intersect_pred = -1\n",
    "        while pred[curr] != -1\n",
    "            curr = pred[curr]\n",
    "            intersect_v = length(intersect(K[v], K[curr]))\n",
    "            if curr_succ != -1\n",
    "                intersect_pred = length(intersect(K[curr], K[curr_succ]))\n",
    "            end\n",
    "            curr_succ = curr\n",
    "            if intersect_v == 0\n",
    "                break\n",
    "            end\n",
    "            #if lastcut were strictly greater, v is not in bouquet\n",
    "            # defined by cut between curr and curr_succ\n",
    "            if intersect_v >= intersect_pred && (isempty(FP) || intersect_v < FP[end])\n",
    "                push!(FP, intersect_v)\n",
    "            end\n",
    "        end\n",
    "        push!(FP, 0)\n",
    "        pmemo = zeros(BigInt, length(FP))\n",
    "        sum += prod * phi(length(K[v]), 1, reverse(FP), fmemo, pmemo)\n",
    "    end\n",
    "    return memo[mapG] = sum\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 55,
   "id": "601689cc",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "MECsize (generic function with 1 method)"
      ]
     },
     "execution_count": 55,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function MECsize(G)\n",
    "    n = nv(G) \n",
    "    memo = Dict{Set, BigInt}() #mapping set of vertices -> AMO sum\n",
    "    fmemo = zeros(BigInt, n)\n",
    "    U = copy(G)\n",
    "    U.ne = 0\n",
    "    for i = 1:n\n",
    "        filter!(j->has_edge(G, j, i), U.fadjlist[i])\n",
    "        filter!(j->has_edge(G, i, j), U.badjlist[i])\n",
    "        U.ne += length(U.fadjlist[i])\n",
    "    end\n",
    "    tres = 1\n",
    "    for component in connected_components(U)\n",
    "        cc = induced_subgraph(U, component)\n",
    "        if !ischordal(cc[1])\n",
    "            println(\"Undirected connected components are NOT chordal...Abort\")\n",
    "            println(\"Are you sure the graph is a CPDAG?\")\n",
    "            # is there anything more clever than just returning?\n",
    "            return\n",
    "        end\n",
    "        tres *= icount(cc, memo, fmemo, 0)\n",
    "    end\n",
    "\n",
    "    return tres\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 64,
   "id": "da3c94db",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1"
      ]
     },
     "execution_count": 64,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "n = 512\n",
    "r = 0.34\n",
    "i = 1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "14121427",
   "metadata": {},
   "outputs": [],
   "source": [
    "G = readgraph(string(, n, \" \", r, \" \", i, \" uccg.in\"), true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f6b41c9c",
   "metadata": {},
   "outputs": [],
   "source": [
    "MECsize(G)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.5",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
